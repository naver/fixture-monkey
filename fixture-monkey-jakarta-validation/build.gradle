import com.github.spotbugs.snom.SpotBugsTask

plugins {
//    id "org.ec4j.editorconfig" version "0.0.3"
    id "com.github.spotbugs" version "4.7.6"
    id "jacoco"
    id "checkstyle"
}

dependencies {
    api(project(":fixture-monkey-api"))
    api("org.hibernate.validator:hibernate-validator:7.0.5.Final")
    api("jakarta.validation:jakarta.validation-api:3.0.2")
    api("org.glassfish:jakarta.el:4.0.2")

    testRuntimeOnly(project(":fixture-monkey-engine"))
    testImplementation(project(":fixture-monkey"))
    testImplementation("org.junit.jupiter:junit-jupiter-engine:${JUNIT_JUPITER_VERSION}")
    testImplementation("org.junit.platform:junit-platform-engine:${JUNIT_ENGINE_VERSION}")
    testImplementation("org.assertj:assertj-core:${ASSERTJ_VERSION}")
    testImplementation("org.projectlombok:lombok:${LOMBOK_VERSION}")
    testAnnotationProcessor("org.projectlombok:lombok:${LOMBOK_VERSION}")
}

// editorconfig {
//    excludes = ["build"]
// }

test {
    useJUnitPlatform {
        includeEngines "jqwik"
    }
}

// check.dependsOn editorconfigCheck

checkstyle {
    configFile = file("${project.rootDir}/tool/naver-checkstyle-rules.xml")
    configProperties = ["suppressionFile": "${project.rootDir}/tool/naver-checkstyle-suppressions.xml"]
    toolVersion = "8.45.1"
    ignoreFailures = false
    maxErrors = 0
    maxWarnings = 0
}

spotbugs {
    ignoreFailures = false
    reportLevel = "high"
    spotbugsTest.enabled = false
}

tasks.withType(SpotBugsTask) {
    reports {
        text.enabled = false
        xml.enabled = true
        html.enabled = false
    }
}

tasks.register("printSpotbugsMain") {
    doLast {
        File mainResult = file("${buildDir}/reports/spotbugs/main.text")
        if (mainResult.exists()) {
            mainResult.readLines().forEach {
                println(it)
            }
        }
    }
}
tasks.getByPath("spotbugsMain").finalizedBy("printSpotbugsMain")

jacoco {
    toolVersion = "0.8.11"
    reportsDirectory.set(file("${buildDir}/reports/jacoco"))
}

jacocoTestReport {
    afterEvaluate {
        classDirectories.setFrom(file("${buildDir}/classes/java/main"))
    }

    reports {
        xml.required = true
        xml.destination file("${buildDir}/reports/jacoco/jacoco.xml")
        csv.required = false
        html.required = true
        html.destination file("${buildDir}/reports/jacoco/html")
    }
}

jacocoTestCoverageVerification {
    afterEvaluate {
        classDirectories.setFrom(file("${buildDir}/classes/main"))
    }

    violationRules {
        rule {
            limit {
                counter = "LINE"
                // minimum = 0.3
            }
        }
    }
}
check.dependsOn jacocoTestCoverageVerification


jar {
    manifest {
        attributes(
                "Specification-Title": artifactName,
                "Specification-Version": project.version,
                "Specification-Vendor": "com.navercorp",
                "Implementation-Title": artifactName,
                "Implementation-Version": project.version,
                "Implementation-Vendor": "com.navercorp"
        )
    }
}
