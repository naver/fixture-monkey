buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "io.github.gradle-nexus:publish-plugin:1.1.0"
    }
}

apply plugin: "io.github.gradle-nexus.publish-plugin"

nexusPublishing {
    repositories {
        sonatype {  //only for users registered in Sonatype after 24 Feb 2021
            nexusUrl.set(uri("https://oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://oss.sonatype.org/content/repositories/snapshots/"))
            username = project.hasProperty("ossrhUsername") ? ossrhUsername : ""
            password = project.hasProperty("ossrhPassword") ? ossrhPassword : ""
        }
    }
}

allprojects {
    apply plugin: "idea"

    repositories {
        mavenCentral()
    }

    group = "com.navercorp.fixturemonkey"
}

subprojects {
    apply plugin: "java-library"
    apply plugin: "maven-publish"
    apply plugin: "signing"

    ext {
        JUNIT_ENGINE_VERSION = "1.9.1"
        JUNIT_JUPITER_VERSION = "5.9.1"
        JQWIK_VERSION = "1.7.3"
        KOTLIN_VERSION = "1.8.0"
    }

    dependencies {
        api("com.google.code.findbugs:jsr305:3.0.2")
        implementation("com.google.code.findbugs:findbugs-annotations:3.0.1")
        compileOnly("org.slf4j:slf4j-api:1.7.25")
        testImplementation("ch.qos.logback:logback-classic:1.2.9")
    }

    tasks.withType(Javadoc).all { enabled = false }

    java {
        withJavadocJar()
        withSourcesJar()
        toolchain {languageVersion = JavaLanguageVersion.of(8)}
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                pom {
                    name = "fixture-monkey"
                    description = "The easiest way to generate controllable arbitrary test objects"
                    url = "http://github.com/naver/fixture-monkey"
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    developers {
                        developer {
                            id = "ah.jo"
                            name = "SeongAh Jo"
                            email = "ah.jo@navercorp.com"
                        }
                        developer {
                            id = "mhyeon-lee"
                            name = "Myeonghyeon Lee"
                            email = "mheyon.lee@gmail.com"
                        }
                    }
                    scm {
                        connection = "scm:git:git://github.com/naver/fixture-monkey.git"
                        developerConnection = "scm:git:ssh://github.com/naver/fixture-monkey.git"
                        url = "http://github.com/naver/fixture-monkey"
                    }
                }
            }
        }
    }

    signing {
        def signingKeyId = findProperty("signingKeyId")
        def signingKey = findProperty("signingKey")
        def signingPassword = findProperty("signingPassword")
        useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
        sign publishing.publications.mavenJava
    }

    tasks.withType(Sign) {
        onlyIf { !version.endsWith("SNAPSHOT") }
    }
}
